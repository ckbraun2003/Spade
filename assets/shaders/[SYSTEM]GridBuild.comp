#version 430

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct Transform {
    vec3 position;
    vec4 rotation;
    vec3 scale;
};

struct GridPair {
    uint cellID;
    uint instanceID;
};

layout(std430, binding = 5) buffer InstanceTransforms {
    Transform instanceTransforms[];
};

layout(std430, binding = 10) buffer GridPairs {
    GridPair gridPairs[];
};

layout(location = 15) uniform float globalBounds;
layout(location = 16) uniform float cellSize;
layout(location = 17) uniform ivec3 gridDim;
layout(location = 18) uniform uint numInstances;

int GetGridIndex(vec3 pos) {
    vec3 offsetPos = pos + vec3(globalBounds);
    ivec3 cell = ivec3(floor(offsetPos / cellSize));
    cell = clamp(cell, ivec3(0), gridDim - ivec3(1));
    return cell.z * (gridDim.x * gridDim.y) + cell.y * gridDim.x + cell.x;
}

void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= numInstances) return;

    vec3 pos = instanceTransforms[index].position;
    uint cellIndex = uint(GetGridIndex(pos));

    gridPairs[index].cellID = cellIndex;
    gridPairs[index].instanceID = index;
}
